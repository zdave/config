#!/usr/bin/env fish

set exit_status 0

function mk-link -a link -a target
    mkdir -p (dirname -- $link)
    if not set -l current (readlink -- $link); or test $current != $target
        if not ln -sT -- $target $link
            set exit_status 1
        end
    end
end

set dir (dirname (readlink -e (status current-filename)))

function mk-plain-link -a path
    mk-link ~/$path $dir/$path
end

function mk-dot-link -a path
    mk-link ~/.$path $dir/$path
end

mkdir -p ~/.ssh/master

mk-plain-link bin/bash-source-env-diff
mk-plain-link bin/calc-replay-gain
mk-plain-link bin/env-diff
mk-plain-link bin/env-dump
mk-plain-link bin/rgit
mk-plain-link bin/rre
mk-plain-link bin/xrandr-normal
if test (hostname -f) = zort.sp4m.net
    mk-plain-link bin/headphones
    mk-plain-link bin/speakers
    mk-plain-link bin/toggle-projector
end

mk-dot-link Xresources
mk-dot-link config/fish
mk-dot-link config/fontconfig
mk-dot-link gitconfig
mk-dot-link gitignore-global
mk-dot-link gmail-smtp-password.gpg
mk-dot-link msmtprc
mk-dot-link parallel/will-cite
mk-dot-link profile
mk-dot-link profile-env
mk-dot-link python-startup.py
mk-dot-link ssh/authorized_keys
mk-dot-link ssh/config
mk-dot-link vim
mk-dot-link vimrc

if which systemctl >/dev/null; and test (count (systemctl --user --no-legend list-unit-files pulseaudio.socket)) -ne 0
    systemctl --user --quiet enable pulseaudio.socket
end

exit $exit_status
